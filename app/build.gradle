apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply from: '../gradle/ktlint.gradle'
apply from: '../gradle/findbugs.gradle'
apply from: '../gradle/jacoco.gradle'

android {
  compileSdkVersion COMPILE_SDK_VERSION as int
  buildToolsVersion BUILD_TOOLS_VERSION

  defaultConfig {
    applicationId PACKAGE_NAME
    minSdkVersion MIN_SDK_VERSION as int
    targetSdkVersion TARGET_SDK_VERSION as int
    versionCode VERSION_CODE as int
    versionName VERSION_NAME
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    multiDexEnabled true
  }

  // SigningConfigs
  apply from: '../keystore/debug.gradle', to: android

  buildTypes {
    debug {
      debuggable true
      zipAlignEnabled true
      testCoverageEnabled false
      applicationIdSuffix ".debug"
      signingConfig signingConfigs.debug
      ext.enableCrashlytics = false
      ext.alwaysUpdateBuildId = false
    }
    release {
      debuggable false
      zipAlignEnabled true
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      shrinkResources true
      // FIXME
      signingConfig signingConfigs.debug
      ext.betaDistributionGroupAliases = "developers"
      ext.betaDistributionReleaseNotes = "git log --oneline".execute().text.split('\n')[0]
    }
  }

  // Variable environment for productFlavors
  apply from: 'flavors.gradle'

  dataBinding {
    enabled = true
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    test.java.srcDirs += ['src/test/kotlin', 'src/debug/kotlin']
  }

  lintOptions {
    lintConfig file('../gradle/android-lint-config.xml')
  }
}

dependencies {
  // Multidex
  implementation 'com.android.support:multidex:1.0.3'
  implementation 'com.android.support:multidex-instrumentation:1.0.3'

  // Google
  implementation "com.android.support:support-compat:$SUPPORT_PACKAGE_VERSION"
  implementation "com.android.support:support-core-utils:$SUPPORT_PACKAGE_VERSION"
  implementation "com.android.support:support-core-ui:$SUPPORT_PACKAGE_VERSION"
  implementation "com.android.support:support-fragment:$SUPPORT_PACKAGE_VERSION"
  implementation "com.android.support:appcompat-v7:$SUPPORT_PACKAGE_VERSION"
  implementation "com.android.support:design:$SUPPORT_PACKAGE_VERSION"
  implementation "com.android.support:customtabs:$SUPPORT_PACKAGE_VERSION"
  implementation "com.android.support:cardview-v7:$SUPPORT_PACKAGE_VERSION"
  implementation "com.android.support:recyclerview-v7:$SUPPORT_PACKAGE_VERSION"
  implementation "com.android.support:support-annotations:$SUPPORT_PACKAGE_VERSION"
  implementation "com.google.android.gms:play-services-base:$GOOGLE_SERVICE_VERSION"
  implementation 'com.android.support.constraint:constraint-layout:1.1.0-beta5'
  implementation 'androidx.core:core-ktx:0.2'
  implementation 'android.arch.lifecycle:extensions:1.1.0'
  kapt 'android.arch.lifecycle:compiler:1.1.0'
  implementation 'android.arch.persistence.room:runtime:1.1.0-alpha3'
  kapt 'android.arch.persistence.room:compiler:1.1.0-alpha3'
  implementation 'android.arch.paging:runtime:1.0.0-alpha6'
  implementation 'android.arch.persistence.room:rxjava2:1.0.0'
  implementation 'android.arch.lifecycle:reactivestreams:1.1.0'
  // FRP
  implementation 'io.reactivex.rxjava2:rxjava:2.1.10'
  implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
  implementation 'io.reactivex.rxjava2:rxkotlin:2.2.0'

  // OkHttp, Retrofit
  implementation 'com.squareup.okhttp3:okhttp:3.10.0'
  implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'
  implementation 'com.squareup.retrofit2:retrofit:2.3.0'
  implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
  // Moshi
  implementation 'com.squareup.moshi:moshi:1.5.0'
  implementation 'com.squareup.moshi:moshi-kotlin:1.5.0'
  implementation 'com.squareup.retrofit2:converter-moshi:2.3.0'
  implementation 'se.ansman.kotshi:api:1.0.2'
  kapt 'se.ansman.kotshi:compiler:1.0.2'
  // Timber
  implementation 'com.jakewharton.timber:timber:4.6.1'

  // Kotlin
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jre8:$KOTLIN_VERSION"

  // Dagger2
  implementation "com.google.dagger:dagger:$DAGGER_VERSION"
  implementation "com.google.dagger:dagger-android:$DAGGER_VERSION"
  implementation "com.google.dagger:dagger-android-support:$DAGGER_VERSION"
  kapt "com.google.dagger:dagger-compiler:$DAGGER_VERSION"
  kapt "com.google.dagger:dagger-android-processor:$DAGGER_VERSION"

  // Glide
  implementation 'com.github.bumptech.glide:glide:4.6.1'
  kapt 'com.github.bumptech.glide:compiler:4.6.1'
  implementation 'com.github.bumptech.glide:okhttp3-integration:4.6.1'
  implementation 'jp.wasabeef:glide-transformations:3.1.1'

  // ReactiveLiveData
  implementation 'com.github.musichin.reactivelivedata:reactivelivedata:0.13.0'
}

// For Debugging
dependencies {
  debugImplementation "com.facebook.stetho:stetho:$STETHO_VERSION"
  debugImplementation "com.facebook.stetho:stetho-okhttp3:$STETHO_VERSION"
  debugImplementation "com.facebook.stetho:stetho-timber:$STETHO_VERSION"
}

// For Testing
dependencies {
  testImplementation "junit:junit:$JUNIT_VERSION"
  testImplementation 'org.robolectric:robolectric:3.7.1'
  testImplementation 'org.mockito:mockito-core:2.15.0'
  testImplementation 'org.mockito:mockito-inline:2.15.0'
  testImplementation 'com.squareup.okhttp3:mockwebserver:3.10.0'

  testImplementation "com.facebook.stetho:stetho:$STETHO_VERSION"
  testImplementation "com.facebook.stetho:stetho-okhttp3:$STETHO_VERSION"
  testImplementation "com.facebook.stetho:stetho-timber:$STETHO_VERSION"
}